# Maintainer: H. Sinan Alioglu <ekpyrosis@gmail.com>
pkgbase=python-tara-py-git
pkgname=('python-tara-py-git' 'python2-tara-py-git')
_pyname=tara-py
_gitname=tara-py
pkgver=20170101
pkgrel=1
pkgdesc='Tara Python Wrapper wit Telegram Bot'
arch=('any')
url='https://github.com/ekpyrosis/tara-py'
license=('BSD')
makedepends=('git' 'python' 'python2' 'python-setuptools' 'python2-setuptools')
options=(!emptydirs)
source=("git+https://github.com/ekpyrosis/${_gitname}")
md5sums=('SKIP')

prepare() {
  cd "${srcdir}/${_gitname}"
  cp -R "${srcdir}/${_gitname}" "${srcdir}/${_gitname}-py2"
}

package_python-tara-py-git() {
  depends=('python' 'python-setuptools')
  provides=('python-tara-py')
  conflicts=('python-tara-py')
  cd "${srcdir}/${_gitname}"
  python3 setup.py install --root="${pkgdir}/" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
}

package_python2-tara-py-git() {
  depends=('python2' 'python2-setuptools')
  provides=('python2-tara-py')
  conflicts=('python2-tara-py')
  cd "${srcdir}/${_gitname}-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
}

pkgver() {
  cd "${srcdir}/${_gitname}"
  git describe --long | sed -E 's/([^-]*-g)/r\1/;s/-/./g;s/^v//'
}

# vim:set ts=2 sw=2 et:
